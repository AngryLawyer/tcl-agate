package require tcltest
namespace import ::tcltest::test
namespace import ::tcltest::cleanupTests

source ../src/agate.tcl

test application {
    We should be able to instantiate an instance of the application class
} -body {
    ::agate::Application app
} -cleanup {
    itcl::delete object app
} -result {app}

test get_post_put_delete {
    We should be able to set GET, POST, PUT, and DELETE paths
} -body {
    ::agate::Application app
    app get "^/$" {{} {}}
    app put "^/$" {{} {}}
    app post "^/$" {{} {}}
    app delete "^/$" {{} {}}
    return [app getRoutes] 
} -cleanup {
    itcl::delete object app
} -result {GET {{^/$} {{} {}}} POST {{^/$} {{} {}}} PUT {{^/$} {{} {}}} DELETE {{^/$} {{} {}}}}

test handle_valid {
    Hitting a standard url should get us a response
} -body {
    ::agate::Application app
    app get "^/$" {{} {return LOL}}
    return [app handle {REQUEST_URI "/" REQUEST_METHOD GET}] 
} -cleanup {
    itcl::delete object app
} -result {LOL}

test handle_404 {
    Hitting a bad URL should throw a 404 at us
} -body {
    ::agate::Application app
    return [app handle {REQUEST_URI "lol/" REQUEST_METHOD GET}] 
} -cleanup {
    itcl::delete object app
} -result {404}

test access_to_request {
    We should have implicit access to $request
} -body {
    ::agate::Application app
    app get "^/$" {{} {return $request}}
    app handle {REQUEST_URI "/" REQUEST_METHOD GET}
} -cleanup {
    itcl::delete object app
} -result {REQUEST_URI "/" REQUEST_METHOD GET}

test access_to_self {
    We should have implicit access to $this
} -body {
    ::agate::Application app
    app get "^/$" {{} {return [$this isa ::agate::Application]}}
    app handle {REQUEST_URI "/" REQUEST_METHOD GET}
} -cleanup {
    itcl::delete object app
} -result {1}

test handle_too_few_params {
    Hitting a url with less parameters than expected should get a response
} -body {
    ::agate::Application app
    app get "^/$" {{first} {return LOL}}
    return [app handle {REQUEST_URI "/" REQUEST_METHOD GET}] 
} -cleanup {
    itcl::delete object app
} -result {LOL}

test handle_too_many_params {
    Hitting a url with more parameters than expected should get a response
} -body {
    ::agate::Application app
    app get {^/([a-z]+)/([a-z]+)/([a-z]+)/$} {{first} {return LOL}}
    return [app handle {REQUEST_URI "/a/a/a/" REQUEST_METHOD GET}] 
} -cleanup {
    itcl::delete object app
} -result {LOL}

cleanupTests
